ACTOR WSAnnihilator 30111 
{
Obituary "The Cyber Annihilator blew %o away with its rockets."
Health 7000 
Radius 40 
Height 110 
Mass 1000 
Speed 16 
PainChance 10 
MONSTER
+NOTARGET 
+FLOORCLIP 
+NORADIUSDMG 
+BOSS 
+MISSILEEVENMORE 
SeeSound "monster/annsit" 
PainSound "cyber/pain" 
DeathSound "monster/anndth" 
ActiveSound "cyber/active" 
Var Int user_level;
Var Int user_ATK;
Var Int user_VIT;
States 
   {    
   Spawn:
       TNT1 A 0 A_SetUserVar("user_ATK",4)
	   TNT1 A 0 A_SetUserVar("user_VIT",6)
	   TNT1 A 1 ACS_NamedExecuteAlways("MonsterStatsint")
   Spawn2:
       ANNI AB 10 A_Look 
       Loop 
   See: 
       ANNI A 3 A_Playsound ("monster/anhoof")
       ANNI ABBCC 3 A_Chase 
       ANNI D 3 A_Playsound ("monster/anhoof") 
       ANNI D 3 A_Chase 
       Loop 
   Missile:
       ANNI E 0 A_Jump (128,11)
       ANNI E 6 A_FaceTarget 
       ANNI F 0 Bright A_CustomMissile ("RocketCyber3",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("RocketCyber3",40,-27,0)
       ANNI E 12 A_FaceTarget 
       ANNI F 0 Bright A_CustomMissile ("RocketCyber3",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("RocketCyber3",40,-27,0) 
       ANNI E 12 A_FaceTarget 
       ANNI F 0 Bright A_CustomMissile ("RocketCyber3",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("RocketCyber3",40,-27,0)
       ANNI E 0 A_Jump (128,1)
       Goto See
       ANNI E 6 A_FaceTarget 
       ANNI F 0 Bright A_CustomMissile ("WSHomRocket1",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("WSHomRocket1",40,-27,0)
       ANNI E 12 A_FaceTarget 
       ANNI F 0 Bright A_CustomMissile ("WSHomRocket1",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("WSHomRocket1",40,-27,0) 
       ANNI E 12 A_FaceTarget 
       ANNI F 0 Bright A_CustomMissile ("WSHomRocket1",40,27,0)
       ANNI F 12 Bright A_CustomMissile ("WSHomRocket1",40,-27,0)  
       Goto See 
   Pain: 
       ANNI G 10 A_Pain 
       Goto See 
   Death:
       TNT1 A 0 ACS_NamedExecuteAlways("levelupcheck",0,150)
	   TNT1 A 0 A_GiveToTarget("StatProgressATK",10)
       ANNI H 10 Bright
       ANNI I 10 Bright A_Scream 
       ANNI JKL 10 Bright
       ANNI M 10 Bright A_NoBlocking 
       ANNI NO 10 Bright
       ANNI P 30 
       ANNI P -1 A_BossDeath 
   } 
}

ACTOR RocketCyber3 : Rocket
{
  Radius 11
  Height 8
  Speed 24
  Damage (ACS_NamedExecuteWithResult("MonDAMAGEProjSCR",20,0,0,0) * Random(1,3))
  Projectile
  +RANDOMIZE
  +DEHEXPLOSION
  +ROCKETTRAIL
  SeeSound "weapons/rocklf"
  DeathSound "weapons/rocklx"
  Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
  States
  {
  Spawn:
    MISL A 1 Bright
    Loop
  Death:
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}

ACTOR WSHomRocket1
{
   Radius 8
   Height 11
   Speed 30
   Damage (ACS_NamedExecuteWithResult("MonDAMAGEProjSCR",8,0,0,0) * Random(1,3))
   PROJECTILE
   DeathSound "weapons/hrlexp"
   ExplosionDamage 64
   ExplosionRadius 96
   +SeekerMissile
   +Rockettrail
   +THRUGHOST
   SeeSound "weapons/hrlfir"
   Decal Scorch
   States
   {
   Spawn:
      HMIS A 4 Bright A_SeekerMissile (25,35)
      HMIS A 4 Bright
      HMIS A 2 Bright A_SeekerMissile (10,25)
      HMIS A 2 Bright
      goto Spawn+2
   Death:
      BAL3 C 0 Bright A_SetTranslucent (0.67,1)
      BAL3 C 8 Bright A_Explode
      BAL3 DE 5 Bright
      stop
   }
}

