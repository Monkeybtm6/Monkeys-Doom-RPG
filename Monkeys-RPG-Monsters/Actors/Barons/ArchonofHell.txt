Actor WSArchonOfHell : BaronOfHell Replaces ArchonOfHell
{ 
  Health 2000 
  Radius 24 
  Height 64 
  Mass 1000 
  Speed 8 
  PainChance 20 
  Monster
  MeleeDamage 10
  Obituary "%o was no match for an archon of hell."
  HitObituary "%o was ripped apart by an archon of hell."
  BloodColor "20 60 20"
  +NoTarget
  +FloorClip
  +MissileMore
  +NoRadiusDmg
  +Boss
  +DontHurtSpecies
  SeeSound "monster/ar2sit" 
  PainSound "baron/pain" 
  DeathSound "monster/ar2dth"
  MeleeSound "baron/melee"
  SpawnID 174
  Var Int user_level;
  Var Int user_ATK;
  Var Int user_VIT;
  States 
  { 
  Spawn:
    TNT1 A 0 A_SetUserVar("user_ATK",6)
	TNT1 A 0 A_SetUserVar("user_VIT",8)
	TNT1 A 1 ACS_NamedExecuteAlways("MonsterStatsint")
  Spawn2:
    BOS4 AB 10 A_Look 
    Loop 
  See:
    BOS4 AABBCCDD 3 A_Chase 
    Loop 
  Missile:
    BOS4 B 0 A_Jump(128,15)
    BOS4 EF 8 Bright A_FaceTarget 
    BOS4 G 0 Bright A_CustomMissile("BaronBall",28,0,0,0)
    BOS4 G 0 Bright A_CustomMissile("BaronBall",28,0,-4,0)
    BOS4 G 3 Bright A_CustomMissile("BaronBall",28,0,4,0)
    BOS4 H 3 Bright
    BOS4 H 0 A_Jump(64,8)
    BOS4 IJ 8 Bright A_FaceTarget 
    BOS4 K 0 Bright A_CustomMissile("BaronBall",28,0,0,0)
    BOS4 K 0 Bright A_CustomMissile("BaronBall",28,0,-4,0)
    BOS4 K 3 Bright A_CustomMissile("BaronBall",28,0,4,0)
    BOS4 L 3 Bright
    BOS4 L 0 A_Jump(64,1)
    Goto See
    BOS4 MN 8 Bright A_FaceTarget
    BOS4 O 0 Bright A_Jump(128,3)
    BOS4 O 4 Bright A_CustomMissile("ArchonComet",28,0,0,0)
    BOS4 P 4 Bright
    Goto See
    BOS4 O 0 A_CustomMissile("BaronBall",28,0,-6,0)
    BOS4 O 0 A_CustomMissile("BaronBall",28,0,-4,0)
    BOS4 O 0 A_CustomMissile("BaronBall",28,0,-2,0)
    BOS4 O 0 A_CustomMissile("BaronBall",28,0,2,0)
    BOS4 O 0 A_CustomMissile("BaronBall",28,0,4,0)
    BOS4 O 3 Bright A_CustomMissile("BaronBall",28,0,6,0)
    BOS4 P 3 Bright
    Goto See
  Melee:
    BOS4 EF 8 Bright A_FaceTarget 
    BOS4 G 3 Bright A_MeleeAttack
    BOS4 H 3 Bright
    BOS4 H 0 A_Jump(128,1)
    Goto See
    BOS4 IJ 8 Bright A_FaceTarget 
    BOS4 K 3 Bright A_MeleeAttack
    BOS4 L 3 Bright
    BOS4 L 0 A_Jump(64,1)
    Goto See
    BOS4 MN 8 Bright A_FaceTarget
    BOS4 O 0 Bright A_MeleeAttack
    BOS4 O 3 Bright A_MeleeAttack
    BOS4 P 3 Bright
    Goto See
  Pain: 
    BOS4 Q 2 
    BOS4 Q 2 A_Pain 
    Goto See 
  Death: 
    TNT1 A 0 ACS_NamedExecuteAlways("levelupcheck",0,70)
	TNT1 A 0 A_GiveToTarget("StatProgressATK",Random(1,5))
	BOS4 R 6 Bright 
    BOS4 S 6 Bright A_Scream 
    BOS4 T 6 Bright 
    BOS4 U 6 Bright A_NoBlocking 
    BOS4 VW 6 Bright 
    BOS4 X 6 Bright A_CustomMissile("ArchonSoul",86,0,0,2,90)
    BOS4 YZ 6 Bright 
    BOS4 [ 6 Bright A_Fall 
    Stop
  } 
}

Actor ArchonComet
{
  Radius 8
  Height 12
  Speed 25
  Damage 20
  Scale 1.0
  SpawnID 251
  Projectile
  +ThruGhost
  SeeSound "weapons/firbfi"
  DeathSound "weapons/hellex"
  DontHurtShooter
  DamageType Fire
  States
  {
  Spawn: 
    ARCB AAAABBBBCCCC 1 Bright A_SpawnItemEx("ArchonCometTrail",0,0,0,0,0,0,0,128)
    Loop
  Death:
    ARCB J 0 Bright A_SetTranslucent (0.67,1)
    ARCB J 3 Bright
    ARCB K 3 Bright A_Explode(128,128,0)
    ARCB LMN 3 Bright
    Stop
  }
}

Actor ArchonSoul
{   
  Radius 1
  Height 1
  Speed 3
  Projectile
  RenderStyle Add
  Alpha 0.80
  States
  {
  Spawn:
    BFX1 ABCD 6 Bright
    Stop 
  }
}

Actor ArchonCometTrail
{   
  Radius 3
  Height 3
  Scale 0.75
  Speed 0
  Projectile
  RenderStyle Add
  Alpha 0.67
  States
  {
  Spawn:
    TNT1 A 3 Bright
    ARCB DEFGHI 3 Bright
    Stop 
  }
}

