//pistol

ACTOR MachinePistolWS : Weapon
{
  Weapon.SelectionOrder 1900
  Weapon.SlotNumber 2
  Weapon.AmmoUse 1
  Weapon.AmmoUse2 1
  Weapon.AmmoGive2 0
  Weapon.AmmoType "MPistmag"
  Weapon.AmmoType2 "Clip"
  Weapon.BobSpeed 2.0
  Weapon.BobRangeX 0.90
  Weapon.BobRangeX 0.75
  Weapon.BobStyle Smooth
  Decal BulletChip
  Obituary "$OB_MPPISTOL"
  //+WEAPON.WIMPY_WEAPON
  +Weapon.Ammo_Optional
  Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
  Tag "Machine Pistol"
  States
  {
  Ready:
    MPIS A 1 A_WeaponReady(wrf_allowreload | WRF_ALLOWZOOM)
    Loop
  Deselect:
    MPIS A 1 A_Lower
    Loop
  Select:
    MPIS A 1 A_Raise
    Loop
  Fire:
    MPIS A 0 A_JumpIfNoAmmo("Reload")
    MPIS B 1
    MPIS A 0 //ACS_NamedExecuteAlways("CheckmonHealth")
	MPIS A 0 A_FireBullets (5.6, 3, -1, (ACS_NamedExecuteWithResult("DAMAGESCR",4,0,0) * Random( 1, 3)), "PistPuff", FBF_USEAMMO|FBF_NORANDOM)
    MPIS A 0 A_PlaySound("weapons/PSfire", CHAN_WEAPON)
    TNT1 A 0 A_FireCustomMissile("Tracer",fRandom(4,-4),0,0,fRandom(4,-4))
	TNT1 A 0 A_FireCustomMissile("WSPistCasingSpawner",5,0,8,-9)
	MPIS A 1 A_GunFlash
    MPIS CD 1
    MPIS A 0 A_ReFire
	MPIS EA 1
    Goto Ready

   Reload:
    MPIS A 0 A_JumpIfInventory ("mpistmag", 20, 2)
    MPIS A 0 A_JumpIfInventory("Clip", 1, "ReloadStep")
    MPIS A 0
    goto ready
   Reloadstep:
    MPIR A 2
	MPIR BCD 2
    MPIR D 3
	MPIR D 1 A_playSound("weapons/pistolClipout", CHAN_WEAPON)
	MPIR D 16
   ReloadLoop:
    TNT1 A 0 A_TakeInventory("Clip", 1)
    TNT1 A 0 A_GiveInventory("mpistmag", 1)
    TNT1 A 0 A_JumpIfInventory("mpistmag", 20, "ReloadDone")
    TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
    Goto ReloadDone
   ReloadDone:
    MPIR D 1 A_playSound("weapons/pistolClipin", CHAN_WEAPON)
    MPIR D 3
	MPIR DCBA 2
    goto ready
  
  Zoom:
  DropLoop:
	TNT1 A 0 A_TakeInventory("mpistmag", 1)
    TNT1 A 0 A_GiveInventory("Clip", 1)
    TNT1 A 0 A_JumpIfInventory("mpistmag", 1, "DropLoop")
	goto Dropdone
  DropDone:
	TNT1 A 0 A_SpawnItemEx("MachinePistolPickup",45,0,30,5)
	TNT1 A 0 A_Takeinventory("Wepslot1",1)
	TNT1 A 1 A_Takeinventory("MachinePistolWS",1)
	stop
  
  Flash:
	TNT1 A 0 A_SetPitch(-0.4 + pitch)
    TNT1 A 3 Bright A_Light1
	TNT1 A 0 A_SetPitch(+0.4 + pitch)
    Goto LightDone

  Spawn:
    MPIS F -1
    Stop
  }
}

ACTOR PistPuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  RenderStyle Translucent
  Alpha 0.5
  VSpeed 1
  Mass 5
  States
  {
  Spawn:
    PUFF A 4 Bright
    PUFF B 4
  Melee:
    PUFF CD 4
    Stop
  }
}

actor "MPistmag" : Ammo
{
  Inventory.MaxAmount 20
  +IGNORESKILL
}

Actor "MachinePistolPickup" : CustomInventory
{
  Inventory.PickupMessage "Picked up Machine Pistol"
  Inventory.PickupSound "misc/W_pkup"
  Scale 0.7
  states
  {
  Spawn:
    MPIS F -1
    Stop
  Pickup:
    TNT1 A 0 A_Jumpifinventory("MachinePistolWS",0,"PickupFail")
    TNT1 A 0 A_Jumpifinventory("Wepslot1",0,"PickupFail")
	TNT1 A 0 A_GiveInventory ("MachinePistolWS")
	TNT1 A 0 A_GiveInventory ("Wepslot1")
    stop
  PickupFail:
    TNT1 A 0 A_Print("Light slot full/Already have weapon.",1)
	Fail
  }
}