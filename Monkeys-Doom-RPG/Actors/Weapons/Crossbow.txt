ACTOR CrossbowWS : Weapon
{
  Weapon.SelectionOrder 1297
  Weapon.SlotNumber 3
  Weapon.AmmoUse 1
  Weapon.AmmoGive 0
  Weapon.AmmoType "Shell"
  Weapon.AmmoType2 "Shell"
  Weapon.BobSpeed 2.0
  Weapon.BobRangeX 0.90
  Weapon.BobRangeX 0.75
  Weapon.BobStyle Smooth
  +Weapon.Ammo_Optional
  Inventory.PickupMessage "Picked up Magic Crossbow"
  Tag "Magic Crossbow"
  States
  {
  Spawn:
    WBOW A -1
    Stop
  Ready:
    CRBW AAAAAABBBBBBCCCCCC 1 A_WeaponReady(WRF_ALLOWZOOM)
    Loop
  Deselect:
    CRBW A 1 A_Lower
    Loop
  Select:
    CRBW A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_JumpIfInventory("Mana",5,"ConfirmFire")
    TNT1 A 0 A_Print("Needs 5 mana")
	Goto Ready
  ConfirmFire:
	TNT1 A 0 A_Jumpifnoammo ("Ready")
	CRBW D 6
    TNT1 A 0 A_FireCustomMissile ("CrossbowFX1", 0, 1, 0, 3)
    TNT1 A 0 A_FireCustomMissile ("CrossbowFX3", -4.5, 0, -2, 3)
    TNT1 A 0 A_FireCustomMissile ("CrossbowFX3",  4.5, 0,  2, 3)
	TNT1 A 0 A_GiveInventory("StatProgressINT",Random(1,5))
	TNT1 A 0 A_TakeInventory("Mana",5)
	CRBW EFGH 3
    CRBW AB 4
    CRBW C 5 A_ReFire
    Goto Ready
  
  Zoom:
	TNT1 A 0 A_SpawnItemEx("CrossbowPickup",45,0,30,5)
	TNT1 A 0 A_Takeinventory("Wepslot2",1)
	TNT1 A 1 A_Takeinventory("CrossbowWS",1)
	stop
  
  }
}

ACTOR CrossbowFX1
{
  Radius 11
  Height 8
  Speed 30
  Damage (ACS_NamedExecuteWithResult("PROJDAMAGESCR",10,0,0,0) * Random(1,3))
  Projectile
  RenderStyle Add
  SeeSound "weapons/bowshoot"
  DeathSound "weapons/bowhit"
  Obituary "$OB_MPCROSSBOW"
  States
  {
  Spawn:
    FX03 B 1 Bright
    Loop
  Death:
    FX03 HIJ 8 Bright
    Stop
  }
}

ACTOR CrossbowFX3 : CrossbowFX1
{
  Speed 20
  Damage (ACS_NamedExecuteWithResult("PROJDAMAGESCR",4,0,0,0) * Random(1,3))
  SeeSound ""
  -NOBLOCKMAP
  +WINDTHRUST
  +THRUGHOST
  States
  {
  Spawn:
    FX03 A 1 Bright
    Loop
  Death:
    FX03 CDE 8 Bright
    Stop
  }
}

Actor "CrossbowPickup" : CustomInventory
{
  Inventory.PickupMessage "Picked up Magic Crossbow"
  Inventory.PickupSound "misc/W_pkup"
  states
  {
  Spawn:
    WBOW A -1
    Stop
  Pickup:
    TNT1 A 0 A_Jumpifinventory("CrossbowWS",0,"PickupFail")
    TNT1 A 0 A_Jumpifinventory("Wepslot2",0,"PickupFail")
	TNT1 A 0 A_GiveInventory ("CrossbowWS")
	TNT1 A 0 A_GiveInventory ("Wepslot2")
    stop
  PickupFail:
    TNT1 A 0 A_Print("Slot 2 full/Already have weapon.",1)
	Fail
  }
}