ACTOR TechShotgunWS : Shotgun
{
  Weapon.SelectionOrder 1299
  Weapon.AmmoUse 1
  Weapon.AmmoUse2 1
  Weapon.AmmoGive 0
  Weapon.AmmoGive2 8
  Weapon.AmmoType "TSGMAG"
  Weapon.AmmoType2 "Shell"
  Weapon.SlotNumber 3
  Weapon.KickBack 60
  Weapon.BobSpeed 2.0
  Weapon.BobRangeX 0.90
  Weapon.BobRangeX 0.75
  Weapon.BobStyle Smooth
  Weapon.SelectionOrder 1300
  Decal BulletChip
  Inventory.PickupMessage "$GOTSHOTGUN"
  Obituary "$OB_MPSHOTGUN"
  DamageType "pierce"
  +Weapon.Ammo_Optional
  +INVENTORY.ALWAYSPICKUP
  Tag "Tech Shotgun"
  States
  {
  Ready:
    TSGN A 1 A_WeaponReady(wrf_allowreload | wrf_allowzoom)
    Loop
  
  Deselect:
    TSGN A 1 A_Lower
    Loop
  
  Select:
    TSGN A 1 A_Raise
    Loop
  
  Fire:
    TSGN A 0 A_JumpIfnoammo("Reload")
    TSGF A 0 A_FireBullets (3.8, 3.4, 7, (ACS_NamedExecuteWithResult("DAMAGESCR",6,0,0) * Random( 1, 3)), "BulletPuff", FBF_USEAMMO|FBF_NORANDOM)
    TSGF A 0 A_PlaySound ("weapons/SGfire", CHAN_WEAPON)
    TSGF AAAAA 0 A_FireCustomMissile("Tracer",fRandom(3,-3),0,0,fRandom(3,-3))
    TSGF A 0 A_GunFlash
	TSGF AB 4 Bright A_Light1
    TSGN B 2 Offset(0,34)
	TSGN B 2 Offset(0,36)
	TSGN C 2 Offset(0,42)
	TSGN C 2 Offset(0,44)
	TSGN C 2 Offset(0,48)
	TSGN D 2 Offset(0,49)
	TSGN D 2 Offset(0,50)
	TNT1 A 0 A_FireCustomMissile("WSShellCasingSpawner",5,0,0,-16)
	TSGN D 2 Offset(0,49)
	TSGN D 2 Offset(0,48)
	TSGN C 2 Offset(0,44)
	TSGN C 2 Offset(0,42)
	TSGN C 2 Offset(0,38)
	TSGN B 2 Offset(0,34)
	TSGN B 2 Offset(0,32)
	TSGN A 7 A_ReFire
    Goto Ready
  
   Reload:
    PKPI A 0 A_JumpIfInventory ("TSGMAG", 12, 2)
    PKPI A 0 A_JumpIfInventory("shell", 1, "ReloadStep")
    PKPI A 0
    goto ready
   Reloadstep:
    TSGR AAABBBCC 1
   ReloadLoop:
    TSGR D 3 A_playsound("weapons/SGLOAD", CHAN_WEAPON)
    TSGR C 6
	TNT1 A 0 A_TakeInventory("shell", 1)
	TNT1 A 0 A_GiveInventory("TSGMAG", 1)
	TNT1 A 0 A_JumpIfInventory("TSGMAG", 12, "ReloadDone")
	TNT1 A 0 A_JumpIfInventory("shell", 1, "ReloadLoop")
	Goto ReloadDone
   ReloadDone:
	TSGR CCBBBAAA 1
	goto ready
  
  Flash:
	TNT1 A 1 Bright A_Light1
    TNT1 A 0 Radius_Quake(2,3,0,1,0)
	TNT1 A 0 A_SetPitch(-0.8 + pitch)
	TNT1 B 1 Bright A_Light2
	TNT1 A 0 A_SetPitch(+0.8 + pitch)
    stop
  
  Zoom:
  DropLoop:
	TNT1 A 0 A_TakeInventory("TSGmag", 1)
    TNT1 A 0 A_GiveInventory("shell", 1)
    TNT1 A 0 A_JumpIfInventory("TSGmag", 1, "DropLoop")
	goto Dropdone
  DropDone:
	TNT1 A 0 A_SpawnItemEx("TechShotgunPickup",45,0,30,5)
	TNT1 A 0 A_Takeinventory("Wepslot2",1)
	TNT1 A 1 A_Takeinventory("TechShotgunWS",1)
	stop
  
  Spawn:
    TSOT A -1
    Stop
  }
}

actor TSGMAG : ammo
{
 Inventory.MaxAmount 12
 +IGNORESKILL
}

Actor "TechShotgunPickup" : CustomInventory
{
  Inventory.PickupMessage "Picked up Tech Shotgun"
  Inventory.PickupSound "misc/W_pkup"
  states
  {
  Spawn:
    TSOT A -1
    Stop
  Pickup:
    TNT1 A 0 A_Jumpifinventory("TechShotgunWS",0,"PickupFail")
    TNT1 A 0 A_Jumpifinventory("Wepslot2",0,"PickupFail")
	TNT1 A 0 A_GiveInventory ("TechShotgunWS")
	TNT1 A 0 A_GiveInventory ("Wepslot2")
    stop
  PickupFail:
    TNT1 A 0 A_Print("Primary slot full/Already have weapon.",1)
	Fail
  }
}