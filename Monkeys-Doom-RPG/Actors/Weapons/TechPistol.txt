//pistol

ACTOR TechPistolWS : Weapon
{
  Weapon.SelectionOrder 1900
  Weapon.SlotNumber 2
  Weapon.AmmoUse 1
  Weapon.AmmoUse2 1
  Weapon.AmmoGive2 0
  Weapon.AmmoType "TechPistmag"
  Weapon.AmmoType2 "Clip"
  Weapon.BobSpeed 2.0
  Weapon.BobRangeX 0.90
  Weapon.BobRangeX 0.75
  Weapon.BobStyle Smooth
  Decal BulletChip
  Obituary "$OB_MPPISTOL"
  +WEAPON.WIMPY_WEAPON 
  +Weapon.Ammo_Optional
  Inventory.Pickupmessage "Picked up Tech Pistol"
  Tag "Tech Pistol"
  States
  {
  Ready:
    DPIG A 1 A_WeaponReady(wrf_allowreload | WRF_ALLOWZOOM)
    Loop
  Deselect:
    DPIG A 1 A_Lower
    Loop
  Select:
    DPIG A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_JumpIfNoAmmo("Reload")
    TNT1 A 0 
	TNT1 A 0 A_FireBullets (3.4, 1.0, 1, (ACS_NamedExecuteWithResult("DAMAGESCR",10,0,0) * Random( 1, 2)), "PistPuff", FBF_USEAMMO|FBF_NORANDOM)
    TNT1 A 0 A_FireCustomMissile("Tracer",fRandom(3,-3),0,0,fRandom(3,-3))
	TNT1 A 0 A_PlaySound("weapons/TPISF", CHAN_WEAPON)
    DPIF A 3 A_GunFlash
    DPIF BCD 1
    DPIF A 0 A_ReFire
    Goto Ready

   Reload:
    PKPI A 0 A_JumpIfInventory ("Techpistmag", 10, 1)
    PKPI A 0 A_JumpIfInventory("Clip", 1, "ReloadStep")
    PKPI A 0
    goto ready
   Reloadstep:
    DPIG A 2 Offset(4,40)
	DPIG A 2 Offset(8,48)
	DPIG A 2 Offset(12,58)
	DPIG A 2 Offset(16,68)
	DPIG A 2 Offset(20,78)
	DPIG A 2 Offset(20,88)
	DPIG A 2 Offset(20,98)
	DPIG A 1 A_playSound("weapons/pistolClipout", CHAN_WEAPON)
	DPIG A 16
   ReloadLoop:
    TNT1 A 0 A_TakeInventory("Clip", 1)
    TNT1 A 0 A_GiveInventory("techpistmag", 1)
    TNT1 A 0 A_JumpIfInventory("techpistmag", 10, "ReloadDone")
    TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
    Goto ReloadDone
   ReloadDone:
	DPIG A 1 A_playSound("weapons/pistolClipin")
    DPIG A 6 Offset(20,98)
	DPIG A 2 Offset(20,88)
	DPIG A 2 Offset(20,78)
	DPIG A 2 Offset(16,68)
	DPIG A 2 Offset(12,58)
	DPIG A 2 Offset(8,48)
    DPIG A 2 Offset(4,40)
    DPIG A 2 Offset(0,32)
    goto ready
  
  Zoom:
	TNT1 A 0 A_SpawnItemEx("TechPistolPickup",45,0,30,5)
	TNT1 A 0 A_Takeinventory("Wepslot1",1)
	TNT1 A 1 A_Takeinventory("TechPistolWS",1)
	stop
  
  Flash:
    TNT1 A 0 A_SetPitch(-0.4 + pitch)
    TNT1 A 2 Bright A_Light1
    TNT1 A 0 A_SetPitch(+0.4 + pitch)
	Goto LightDone

  Spawn:
    DEPI A -1
    Stop
  
  }
}

actor "TechPistmag" : Ammo
{
  Inventory.MaxAmount 10
  +IGNORESKILL
}

Actor "TechPistolPickup" : CustomInventory
{
  Inventory.PickupMessage "$PICKUP_PISTOL_DROPPED"
  Inventory.PickupSound "misc/W_pkup"
  states
  {
  Spawn:
    DEPI A -1
    Stop
  Pickup:
    TNT1 A 0 A_Jumpifinventory("TechPistolWS",0,"PickupFail")
    TNT1 A 0 A_Jumpifinventory("Wepslot1",0,"PickupFail")
	TNT1 A 0 A_GiveInventory ("TechPistolWS")
	TNT1 A 0 A_GiveInventory ("Wepslot1")
    stop
  PickupFail:
    TNT1 A 0 A_Print("Light slot full/Already have weapon.",1)
	Fail
  }
}