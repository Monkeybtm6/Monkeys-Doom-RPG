ACTOR SuperShotgunWS : SuperShotgun
{
  Weapon.SelectionOrder 400
  Weapon.SlotNumber 3
  Weapon.AmmoUse 2
  Weapon.AmmoGive 0
  Weapon.AmmoType "Shell"
  Weapon.AmmoType2 "Shell"
  Weapon.KickBack 120
  Weapon.BobSpeed 2.0
  Weapon.BobRangeX 0.90
  Weapon.BobRangeX 0.75
  Weapon.BobStyle Smooth
  Decal BulletChip
  Inventory.PickupMessage "$GOTSHOTGUN2"
  Obituary "$OB_MPSSHOTGUN"
  Tag "$TAG_SUPERSHOTGUN"
  States
  {
  Ready:
    SHT2 A 1 A_WeaponReady(wrf_allowzoom)
    Loop
  
  Deselect:
    SHT2 A 1 A_Lower
    Loop
  
  Select:
    SHT2 A 1 A_Raise
    Loop
  
  Fire:
    TNT1 A 0
	SHT2 A 3
    SHT2 A 0 A_FireBullets (11.2, 7.1, 20, (ACS_NamedExecuteWithResult("DAMAGESCR",12,0,0)), "BulletPuff", FBF_USEAMMO|FBF_NORANDOM)
    SHT2 A 0 A_PlaySound ("weapons/SSGFIRE", CHAN_WEAPON)
    SHT2 A 0 A_ZoomFactor(0.975)
	SHT2 A 7 A_GunFlash
    SHT2 B 1 A_ZoomFactor(0.985)
    SHT2 B 1 A_ZoomFactor(0.995)
	SHT2 B 6 A_ZoomFactor(1.0)
	SHT2 C 7 A_CheckReload
    SHT2 D 7 A_OpenShotgun2
    SHT2 E 7
    SHT2 F 7 A_LoadShotgun2
    SHT2 G 6
    SHT2 H 6 A_CloseShotgun2
    SHT2 A 5 A_ReFire
    Goto Ready
	
  Flash:
	SHT2 I 3 Bright A_Light1
    TNT1 A 0 Radius_Quake(3,2,0,1,0)
	SHT2 J 3 Bright A_Light2
	Goto LightDone
  
  Zoom:
	TNT1 A 0 A_SpawnItemEx("SuperShotgunPickup",45,0,30,5)
	TNT1 A 0 A_Takeinventory("Wepslot2",1)
	TNT1 A 1 A_Takeinventory("SuperShotgunWS",1)
	stop
  
  Spawn:
    SGN2 A -1
    Stop
  }
}

Actor "SuperShotgunPickup" : CustomInventory Replaces SuperShotgun
{
  Inventory.PickupMessage "Picked up Super Shotgun"
  Inventory.PickupSound "misc/W_pkup"
  states
  {
  Spawn:
    SGN2 A -1
    Stop
  Pickup:
    TNT1 A 0 A_Jumpifinventory("SuperShotgunWS",0,"PickupFail")
    TNT1 A 0 A_Jumpifinventory("Wepslot2",0,"PickupFail")
	TNT1 A 0 A_GiveInventory ("SuperShotgunWS")
	TNT1 A 0 A_GiveInventory ("Wepslot2")
    stop
  PickupFail:
    TNT1 A 0 A_Print("Slot 2 full/Already have weapon.",1)
	Fail
  }
}