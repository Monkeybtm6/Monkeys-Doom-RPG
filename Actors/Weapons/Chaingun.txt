ACTOR AssaultRifleWS : Weapon
{
   Weapon.SelectionOrder 700
   Weapon.SlotNumber 3
   Weapon.AmmoUse 1
   Weapon.AmmoGive2 0
   Weapon.AmmoType "ARMag"
   Weapon.AmmoType2 "Clip"
   Decal BulletChip
   Inventory.PickupMessage "You got the Assault Rifle" // "You got the chaingun"
   +Weapon.Ammo_Optional
   Obituary "%o was mowed down by %k's Assault Rifle."
   Tag "Assault Rifle"
   States
   {
   
   Ready:
     CHGG A 1 A_WeaponReady(wrf_allowreload | WRF_ALLOWZOOM)
     Loop
   
   Deselect:
     CHGG A 1 A_Lower
     Loop
   
   Select:
     CHGG A 1 A_Raise
     Loop
   
   Fire:
     CHGF A 0 A_JumpIfNoAmmo ("Reload")
	 CHGF A 0 A_PlaySound("Weapons/AssRif1", CHAN_WEAPON)
	 CHGF A 0 Radius_Quake(1,2,0,1,0)
	 CHGF A 0 A_FireBullets(5.6, 2, 1, (ACS_NamedExecuteWithResult("DAMAGESCR",12,0,0)), "BulletPuff", FBF_USEAMMO|FBF_NORANDOM)
	 CHGF A 1 Bright A_Light1
	 CHGF B 2
	 CHGF C 1
     Goto Ready
   
   Reload:
    CHGG A 0 A_JumpIfInventory ("ARMag", 32, 2)
    CHGG A 0 A_JumpIfInventory("Clip", 1, "ReloadStep")
    CHGG A 0
    goto ready
   Reloadstep:
    CHGR SRQPO 2
	CHGR N 1 A_playSound("weapons/pistolclipin")
	CHGR MLKJIHG 2
    CHGR G 3
   ReloadLoop:
	TNT1 A 0 A_TakeInventory("Clip", 1)
    TNT1 A 0 A_GiveInventory("ARMag", 1)
    TNT1 A 0 A_JumpIfInventory("ARMag", 32, "ReloadDone")
    TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
    Goto ReloadDone
   ReloadDone:
	CHGR GHIJKLM 2
	CHGR N 2 A_playSound("weapons/pistolclipin")
	CHGR OP 2
	CGCK ABCDEF 2
	goto ready
   
  Zoom:
	TNT1 A 0 A_SpawnItemEx("AssaultRiflePickup",45,0,30,5)
	TNT1 A 0 A_Takeinventory("Wepslot2",1)
	TNT1 A 1 A_Takeinventory("AssaultRifleWS",1)
	stop
   
   Spawn:
     MGUN A -1
     Stop
   }
}

actor ARMAG : ammo
{
 Inventory.MaxAmount 32
 +IGNORESKILL
}

Actor "AssaultRiflePickup" : CustomInventory Replaces Chaingun
{
  Inventory.PickupMessage "Picked Up Assault Rifle"
  Inventory.PickupSound "misc/W_pkup"
  states
  {
  Spawn:
    MGUN A -1
    Stop
  Pickup:
    TNT1 A 0 A_Jumpifinventory("AssaultRifleWS",0,"PickupFail")
    TNT1 A 0 A_Jumpifinventory("Wepslot2",0,"PickupFail")
	TNT1 A 0 A_GiveInventory ("AssaultRifleWS")
	TNT1 A 0 A_GiveInventory ("Wepslot2")
    stop
  PickupFail:
    TNT1 A 0 A_Print("Slot 2 full/Already have weapon.",1)
	Fail
  }
}